plugins {
	id 'java'
	id 'war'
	id 'eclipse-wtp'
	id 'org.iplass.dev' version '1.0.0'
}

ext {
	javaVersion = JavaVersion.VERSION_21
}

repositories {
	mavenCentral()

	if (project.hasProperty('iPLAssMavenRepoUrl')) {
		maven {
			url iPLAssMavenRepoUrl
		}
	}
}

dependencies {
	implementation platform("org.iplass:iplass-bom:${iPLAssVersion}")

	implementation 'org.iplass:iplass-core'
	implementation 'org.iplass:iplass-gem'
	implementation 'org.iplass:iplass-web'

	runtimeOnly 'org.iplass:iplass-admin'
	runtimeOnly 'org.iplass:iplass-tools'

	providedRuntime group: 'org.iplass', name: 'iplass-tools-batch', version: iPLAssVersion, transitive: false

	// Infinispanを利用する場合は以下のコメントアウトを外してください。
//	runtimeOnly 'org.iplass:iplass-infinispan'

	// Redisを利用する場合は以下のコメントアウトを外してください。
//	runtimeOnly 'org.iplass:iplass-redis'

	// GoogleCloudを利用する場合は以下のコメントアウトを外してください。
//	runtimeOnly 'org.iplass:iplass-googlecloud'
	
	// provided JakartaEE 10 apis
	// Jakarta Servlet
	providedCompile 'jakarta.servlet:jakarta.servlet-api:6.0.0'
	// Jakarta JSP
	providedCompile 'jakarta.servlet.jsp:jakarta.servlet.jsp-api:3.1.1'
	// Jakarta EL
	providedCompile 'jakarta.el:jakarta.el-api:5.0.0'
	
	// Jakarta Annotations
	compileOnly 'jakarta.annotation:jakarta.annotation-api:2.1.1'
	// Jakarta Activation
	compileOnly 'jakarta.activation:jakarta.activation-api:2.1.3'
	// Jakarta jstl
	compileOnly 'jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api:3.0.0'
	// Jakarta rest
	compileOnly 'jakarta.ws.rs:jakarta.ws.rs-api:3.1.0'
	// Jakarta Mail
	compileOnly 'jakarta.mail:jakarta.mail-api:2.1.3'
	// Jakarta Validation
	compileOnly 'jakarta.validation:jakarta.validation-api:3.0.2'
	// Jakarta xml binding
	compileOnly 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'

	// Jakarta Activation runtime
	runtimeOnly 'org.eclipse.angus:angus-activation:2.0.2'
	// Jakarta jstl runtime
	runtimeOnly 'jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api:3.0.0'
	runtimeOnly 'org.glassfish.web:jakarta.servlet.jsp.jstl:3.0.1'
	// Jakarta rest runtime
	runtimeOnly platform('org.glassfish.jersey:jersey-bom:3.1.7')
	runtimeOnly 'org.glassfish.jersey.containers:jersey-container-servlet'
	runtimeOnly 'org.glassfish.jersey.media:jersey-media-jaxb'
	runtimeOnly 'org.glassfish.jersey.media:jersey-media-json-jackson'
	runtimeOnly 'org.glassfish.jersey.media:jersey-media-multipart'
	runtimeOnly 'org.glassfish.jersey.inject:jersey-hk2'
	runtimeOnly 'com.fasterxml.jackson.jakarta.rs:jackson-jakarta-rs-json-provider'
	// Jakarta Mail runtime
	runtimeOnly 'org.eclipse.angus:angus-mail:2.0.3'
	// Jakarta Validation runtime
	runtimeOnly 'jakarta.validation:jakarta.validation-api:3.0.2'
	runtimeOnly 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
	// Jakarta xml binding runtime
	runtimeOnly 'org.glassfish.jaxb:jaxb-runtime:4.0.5'

	// NOTE: Bundled with tomcat, required for batch.
	// Jakarta EL runtime
	runtimeOnly 'org.glassfish.expressly:expressly:5.0.0'

	testImplementation 'org.iplass:iplass-test'
	testImplementation platform('org.junit:junit-bom:5.10.2')
	testImplementation 'org.junit.jupiter:junit-jupiter'

	// 以下のJDBCドライバはテナント作成ツール(runTenantBatchタスク)の実行に必要となります。
	// 実行する場合は使用するJDBCドライバのコメントアウトを外してください。
	// Maven Cetnral Repository に登録されていないJDBCドライバについては'lib/jdbc'に配置してください。
//	providedRuntime 'com.mysql:mysql-connector-j:9.0.0'
//	providedRuntime 'org.postgresql:postgresql:42.7.3'
//	providedRuntime 'com.microsoft.sqlserver:mssql-jdbc:12.6.3.jre11'
//	providedRuntime 'com.oracle.database.jdbc:ojdbc8:23.4.0.24.05'

	providedRuntime fileTree('lib').include('**/*.jar')
}

java {
	sourceCompatibility = javaVersion
	targetCompatibility = javaVersion
}

tasks.withType(AbstractCompile) {
	options.encoding = 'UTF-8'
}

tasks.withType(GroovyCompile) {
	options.encoding = 'UTF-8'
}

tasks.withType(JavaExec) {
	if (System.getProperty('mtp.config') != null) {
		systemProperty 'mtp.config', System.getProperty('mtp.config')
	}
}

test {
	useJUnitPlatform()
}

// テナント作成ツール実行
task runTenantBatch(type: JavaExec, group: 'application', description : 'execute tenant batch.') {
	mainClass = "org.iplass.mtp.tools.batch.tenant.TenantBatch"
	classpath = sourceSets.main.runtimeClasspath
}

// Entity Java マッピングクラス作成ツール実行
task createEntityClass(type: JavaExec, group: 'application', description : 'create entity java class.') {
	mainClass = "org.iplass.mtp.tools.batch.entity.EntityJavaMappingClassCreator"
	classpath = sourceSets.main.runtimeClasspath
	standardInput = System.in
}

// マージ済service-config表示ツール実行
//   引数：
//     -m [モード]   : モード(PARSE_ONLY:パースのみ、PARSE_LOAD:パース及びサービスのロード)、未指定時はPARSE_ONLY
//     -o [ファイル] : 出力ファイル名、未指定時はコンソール出力
//   実行例１：パースのみ、コンソール出力
//     gradlew viewMergedServiceConfig
//   実行例２：パース及びサービスのロード、'skeleton-service-config.xml'ファイルへ出力
//     gradlew viewMergedServiceConfig --args="-m PARSE_LOAD -o skeleton-service-config.xml"
task viewMergedServiceConfig(type: JavaExec, group: 'application', description : 'view merged service-config.') {
	mainClass = "org.iplass.mtp.tools.batch.config.ServiceConfigViewer"
	classpath = sourceSets.main.runtimeClasspath
}

// ===============================
//  org.iplass.dev プラグイン設定
// ===============================
//configure(iplass) {
//	// required
//	tenantId = TENANT_ID // change your tenant id
//
//	// optional
//	//serviceConfig = '/mtp-service-config.xml'
//	//language = 'system'
//	//classpath = sourceSets.main.runtimeClasspath
//}
//
//metaSyncRdbToFile {
//	// RDB管理しているメタデータパスを入力する。
//	source = ['/entity/*', '/staticresource/*']
//}
